# World2 code with pydynamo syntax
# FROM https://cress.soc.surrey.ac.uk/s4ss/code/Codesegments/chapter3.html
p.k = p.j+dt*(br.j - dr.j)
p.i = pi
pi = 1.65e9
br.k = p.k*clip(brn,brn1,swt1,time.k)*brfm.k*brmm.k*brcm.k*brpm.k
brn = 0.04
brn1 = 0.04
swt1 = 1970
brmm.k = tabhl(brmmt,msl.k,0,5,1)
brmmt =[1.2, 1, 0.85, 0.75, 0.7, 0.7]
msl.k = ecir.k/ecirn
ecirn = 1
ecir.k = cir.k*(1-ciaf.k)*nrem.k/(1-ciafn)
nrem.k = tabhl(nremt,nrfr.k,0,1,0.25)
nremt =[0, 0.15, 0.5, 0.85, 1]
nrfr.k = nr.k/nri
nr.k = nr.j-dt*nrur.j
nr.i = nri
nri = 900e9
nrur.k = p.k*clip(nrun,nrun1,swt2,time.k)*nrmm.k
nrun = 1
nrun1 = 1
swt2 = 1970
dr.k = p.k*clip(drn,drn1,swt3,time.k)*drmm.k*drpm.k*drfm.k*drcm.k
drn = 0.028
drn1 = 0.028
swt3 = 1970
drmm.k = tabhl(drmmt,msl.k,0,5,0.5)
drmmt =[3, 1.8, 1, 0.8, 0.7, 0.06, 0.53, 0.5, 0.5, 0.5, 0.5]
drpm.k = tabhl(drpmt,polr.k,0,60,10)
drpmt =[0.92, 1.3, 2, 3.2, 4.8, 6.8, 9.2]
drfm.k = tabhl(drfmt,fr.k,0,2,0.25)
drfmt =[30, 3, 2, 1.4, 1, 0.7, 0.6, 0.5, 0.5]
drcm.k = tabhl(drcmt,cr.k,0,5,1)
drcmt =[0.9, 1, 1.2, 1.5, 1.9, 3]
cr.k = p.k/(la*pdn)
la = 135e6
pdn = 26.5
brcm.k = tabhl(brcmt,cr.k,0,5,1)
brcmt =[1.05, 1, 0.9, 0.7, 0.6, 0.55]
brfm.k = tabhl(brfmt,fr.k,0,4,1)
brfmt =[0, 1, 1.6, 1.9, 2]
brpm.k = tabhl(brpmt,polr.k,0,60,10)
brpmt =[1.02, 0.9, 0.7, 0.4, 0.25, 0.15, 0.1]
fr.k = fpci.k*fcm.k*fpm.k*clip(fc,fc1,swt7,time.k)/fn
fc = 1
fc1 = 1
fn = 1
swt7 = 1970
fcm.k = tabhl(fcmt,cr.k,0,5,1)
fcmt =[2.4, 1, 0.6, 0.4, 0.3, 0.2]
fpci.k = tabhl(fpcit,cira.k,0,6,1)
fpcit =[0.5, 1, 1.4, 1.7, 1.9, 2.05, 2.2]
cira.k = cir.k*ciaf.k/ciafn
ciafn = 0.3
cir.k = ci.k/p.k
ci.k = ci.j+dt*(cig.j - cid.j)
ci.i = cii
cii = 0.4e9
cig.k = p.k*cim.k*clip(cign,cign1,swt4,time.k)
cign = 0.05
cign1 = 0.05
swt4 = 1970
cim.k = tabhl(cimt,msl.k,0,5,1)
cimt =[0.1, 1, 1.8, 2.4, 2.8, 3]
cid.k = ci.k*clip(cidn,cidn1,swt5,time.k)
cidn = 0.025
cidn1 = 0.025
swt5 = 1970
fpm.k = tabhl(fpmt,polr.k,0,60,10)
fpmt =[1.02, 0.9, 0.65, 0.35, 0.2, 0.1, 0.05]
polr.k = pol.k/pols
pols = 3.6e9
pol.k = pol.j+dt*(polg.j-pola.j)
pol.i = poli
poli = 0.2e9
polg.k = p.k*clip(poln,poln1,swt6,time.k)*polcm.k
poln = 1
poln1 = 1
swt6 = 1970
polcm.k = tabhl(polcmt,cir.k,0,5,1)
polcmt =[0.05, 1, 3, 5.4, 7.4, 8]
pola.k = pol.k/polat.k
polat.k = tabhl(polatt,polr.k,0,60,10)
polatt =[0.6, 2.5, 5, 8, 11.5, 15.5, 20]
ciaf.k = ciaf.j+dt/ciaft*(cfifr.j*ciqr.j-ciaf.j)
ciaf.i = ciafi
ciafi = 0.2
ciaft = 15
cfifr.k = tabhl(cfifrt,fr.k,0,2,0.5)
cfifrt =[1, 0.6, 0.3, 0.15, 0.1]
ql.k = qls*qlm.k*qlc.k*qlf.k*qlp.k
qls = 1
qlm.k = tabhl(qlmt,msl.k,0,5,1)
qlmt =[0.2, 1, 1.7, 2.3, 2.7, 2.9]
qlc.k = tabhl(qlct,cr.k,0,5,0.5)
qlct =[2, 1.3, 1, 0.75, 0.55, 0.45, 0.38, 0.3, 0.25, 0.22, 0.2]
qlf.k = tabhl(qlft,fr.k,0,4,1)
qlft =[0, 1, 1.8, 2.4, 2.7]
qlp.k = tabhl(qlpt,polr.k,0,60,10)
qlpt =[1.04, 0.85, 0.6, 0.3, 0.15, 0.05, 0.02]
nrmm.k = tabhl(nrmmt,msl.k,0,10,1)
nrmmt =[0, 1, 1.8, 2.4, 2.9, 3.3, 3.6, 3.8, 3.9, 3.95, 4]
ciqr.k = tabhl(ciqrt,qlm.k/qlf.k,0,2,0.5)
ciqrt =[0.7, 0.8, 1, 1.5, 2]
dt = 0.1
length = 2100
initial_time = 1900
#End of file !
