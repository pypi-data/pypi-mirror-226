// This grammar describes the content of an xUML Operation (op) file

operation = BLOCK_END ee_prefix signature BLOCK_END activity? EOF

ee_prefix = op_type SP* ee_name ('<' cname '>')? '.'
ee_name = icaps_name
cname = icaps_name
op_type = '>>' / '<<'
signature = op_name input_parameters output_type? NL
op_name = icaps_name
input_parameters = '(' SP? (parameters? SP?) ')'
output_type = ' : ' icaps_all_name
parameters = parameter (', ' parameter)*
parameter = phrase SP? ':' SP? icaps_all_name

activity = body_line*
body_line = r'.*' NL // An unstructured line of text

phrase = lword (DELIM lword)*  // Relationship phrase
icaps_all_name = iword (DELIM iword)*
icaps_name = iword (DELIM word)*  // A name using initial caps only
word = iword / lword
iword = r'[A-Z][A-Za-z0-9]*' // Initial cap word (used in Class and Subsystem names)
lword = r'[a-z][a-z]*' // Lower case only word (used in relationship phrase)
DELIM = r'[ _]' // Delimiter used inside of names
SP = " " // Single space
NL = "\n" // New line
BLOCK_END = "--" NL?